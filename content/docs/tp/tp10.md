---
title: "Séance 10"
date: 2022-04-19T15:32:27+02:00
draft: false
weight: 10
---

# Séance 10

## Exercice 1
```oz
declare
P S
{NewPort S P}

proc {BrowseStream S}
    proc {BrowseStreamH S}
        case S of H|T then {Browse H} {BrowseStreamH T} end
    end
in
    thread {BrowseStreamH S} end
end

{BrowseStream S}

{Send P foo}
{Send P bar}
```

## Exercice 2
```oz
declare
fun {LaunchServer}
    P S
    proc {HandleStream S}
        case S of H|T then
            case H of add(X Y R) then
                R = X+Y
                {HandleStream T}
            [] pow(X Y R) then
                R={Number.pow X Y}
                {HandleStream T}
            [] 'div'(X Y R) then
                R = X div Y
                {HandleStream T}
            else
                {Browse "je ne comprends pas ton message"}
                {HandleStream T}
            end 
        end
    end
in
    {NewPort S P}
    thread {HandleStream S} end
    P
end
A = {LaunchServer}
R S T
{Send A pow(10 2 R)}
{Send A add(12 18 S)}
{Send A 'div'(12 2 T)}
{Browse R}
{Browse S}
{Browse T}
```

## Exercice 3
```oz
declare
fun {StudentRMI}
    S
in
    thread
        for ask(howmany:Beers) in S do
            Beers={OS.rand} mod 24
        end
    end
    {NewPort S}
end
 
fun {StudentCallBack}
    S
in
    thread
        for ask(howmany:P) in S do
            {Send P {OS.rand} mod 24}
        end
    end
    {NewPort S}
end
 
fun {CreateUniversity Size F}
    fun {CreateLoop I}
        if I =< Size then
 % pour {Student} choisissez soit StudentRMI ou StudentCallBack,
 % défini plus haut, selon l'humeur de Charlotte
            {F}|{CreateLoop I+1}
        else nil end
    end
in
    {CreateLoop 1}
end
 
 
fun {Charlotte UniStr Type} 
    CharlotteStream
    CharlottePort = {NewPort CharlotteStream} 
 
    fun {TotalCallBack UnivStr NumberStr Acc}
        N
    in 
        case UnivStr of Stud1|Studs then 
            {Send Stud1 ask(howmany:CharlottePort)} 
            case NumberStr of Num1|Nums then
                N=Num1
                {TotalCallBack Studs Nums N+Acc} 
            else
                N = 0
                {TotalCallBack Studs nil N+Acc} 
            end 
        else Acc end 
    end
    
    fun {TotalRMI UnivStr Acc} 
        case UnivStr of Stud1|Studs then 
            N
        in 
            {Send Stud1 ask(howmany:N)} 
            {TotalRMI Studs Acc+N} 
        else Acc end 
    end 
in 
    case Type of rmi then 
        {TotalRMI UniStr 0} 
    else 
        {TotalCallBack UniStr CharlotteStream 0} 
    end
end

Uni = {CreateUniversity 10 StudentCallBack}
UniRMI = {CreateUniversity 10 StudentRMI}
Total = {Charlotte Uni student}
TotalRmi = {Charlotte UniRMI rmi}
{Browse Total}
{Browse TotalRmi}
```

## Exercice 4
```oz
declare

fun {NewPortObject Behaviour Init}
    proc {MsgLoop S1 State}
        case S1 of Msg|S2 then
            {MsgLoop S2 {Behaviour Msg State}}
        [] nil then skip
    end
end
    Sin
in
    thread {MsgLoop Sin Init} end
    {NewPort Sin}
end

fun {Portier}
    fun {Counter Msg State}
        case Msg of getIn(N) then
            State + N
        [] getOut(N) then
            State - N
        [] getCount(N) then
            N = State
            State
        else
            State
        end
    end
in
    {NewPortObject Counter 0}
end

O = {Portier}
{Send O getIn(10)}
{Send O getOut(2)}
X
{Send O getCount(X)}
{Browse X}
```

## Exercice 5
```oz
declare
fun {NewPortObject Behaviour Init}
    proc {MsgLoop S1 State}
        case S1 of Msg|S2 then
            {MsgLoop S2 {Behaviour Msg State}}
        [] nil then skip
    end
end
    Sin
in
    thread {MsgLoop Sin Init} end
    {NewPort Sin}
end

fun {NewStack}
    fun {Stack Msg State}
        case Msg of push(N) then
            N|State
        [] pop(N) then
            case State of nil then N=nil nil
            [] H|T then
                N=H
                T
            end
        [] isEmpty(N) then
            N = State == nil
            State
        else
            State
        end
    end
in
    {NewPortObject Stack nil}
end

proc {Push P X}
    {Send P push(X)}
end

proc {Pop P X}
    {Send P pop(X)}
end

proc {IsEmpty P X}
    {Send P isEmpty(X)}
end

Stack = {NewStack}
{Push Stack 1}
{Push Stack 2}
{Push Stack 3}
F = {IsEmpty Stack}
A = {Pop Stack}
B = {Pop Stack}
C = {Pop Stack}
T = {IsEmpty Stack}

{Browse F}
{Browse A}
{Browse B}
{Browse C}
{Browse T}
```

## Exercice 6
```oz
declare
fun {NewPortObject Behaviour Init}
    proc {MsgLoop S1 State}
        case S1 of Msg|S2 then
            {MsgLoop S2 {Behaviour Msg State}}
        [] nil then skip
    end
end
    Sin
in
    thread {MsgLoop Sin Init} end
    {NewPort Sin}
end

fun {NewQueue}
    fun {Queue Msg State}
        case Msg of enqueue(N) then
            N|State
        [] dequeue(N) then
            case State 
            of nil then
                N = nil
                nil 
            [] _|_ then
                Reversed
            in
                Reversed = {List.reverse State}
                N = Reversed.1
                {List.reverse Reversed.2}
            end 
        [] isEmpty(N) then
            N = State == nil
            State
        [] getElements(N) then
            N = State
            State
        else
            State
        end
    end
in
    {NewPortObject Queue nil}
end

proc {Enqueue P X}
    {Send P enqueue(X)}
end

proc {Dequeue P X}
    {Send P dequeue(X)}
end

proc {IsEmpty P X}
    {Send P isEmpty(X)}
end

proc {GetElements P X}
    {Send P getElements(X)}
end

Queue = {NewQueue}
{Enqueue Queue 1}
{Enqueue Queue 2}
{Enqueue Queue 3}
E = {GetElements Queue}
F = {IsEmpty Queue}
A = {Dequeue Queue}
B = {Dequeue Queue}
C = {Dequeue Queue}
T = {IsEmpty Queue}

{Browse E}
{Browse F}
{Browse A}
{Browse B}
{Browse C}
{Browse T}
```

## Exercice 7